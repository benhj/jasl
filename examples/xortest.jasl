;;; simple xor cryptor

start {

    ;;; read clear text and key
    args 0 -> input;
    args 1 -> key;

    ;;; convert to byte arrays
    array:byte key -> keybytes;

    ;;; require lengths of clear text and key
    length input -> inlength;
    length keybytes -> keylength;

    ;;; create array to store cipher bytes
    array:byte(inlength) -> outbytes;

    ;;; encrypt 
    prn "Cipher text: ";
    int 0 -> k;
    for i in input {
        int (k % keylength) -> keyval;
        get keybytes(keyval) -> kc;
        byte (i ^ kc) -> xored;
        pr xored;
        put xored -> outbytes(k);
        put (k + 1) -> k;
    }
    prn " ";
    prn "Clear text as decrypted: ";
    ;;; decrypt
    put 0 -> k;
    for i in outbytes {
        int (k % keylength) -> keyval;
        get keybytes(keyval) -> kc;
        byte (i ^ kc) -> xored;
        pr xored;
        put (k + 1) -> k;
    }
    prn " ";

}